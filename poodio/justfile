set shell := ['/usr/bin/env', 'bash', '-cu']
set windows-shell := ['C:/Program Files/Git/bin/bash.exe', '-cu']

build TARGET:
    @just \
        build-bin '{{ TARGET }}' \
        build-npm '{{ TARGET }}' \
        build-pip '{{ TARGET }}'

build-bin TARGET:
    @rm -rf '{{ BIN_DIST }}'
    cargo build --bin poodio --locked \
        --features default --release --target '{{ TARGET }}'
    @mkdir -p '{{ BIN_DIST }}'
    cp '../target/{{ TARGET }}/release/{{ BIN_NAME }}' '{{ BIN_FILE }}'

build-npm TARGET:
    @rm -rf '{{ NPM_DIST }}'
    @echo '{}' > 'package.json'
    npx napi build --cargo-flags='--locked --lib' --no-dts-header --release \
        --features bind-napi --target '{{ TARGET }}' '{{ NPM_DIST }}native/'
    @rm -rf 'package.json'
    cp '{{ NPM_DIST }}native/index.d.ts' '{{ NPM_DIST }}common/'

build-pip TARGET:
    @rm -rf '{{ PIP_DIST }}'
    uv run --no-sync maturin build --bindings pyo3 --locked --release \
        --features bind-pyo3 --target '{{ TARGET }}' --out '{{ PIP_DIST }}'

test-e2e:
    @just \
        test-e2e-bin \
        test-e2e-npm \
        test-e2e-pip \
    && echo '[PASS] test-e2e'

test-e2e-bin:
    @([[ -x '{{ BIN_FILE }}' && ! -d '{{ BIN_FILE }}' ]]) || \
        (echo '[FAIL] permission' && exit 2)
    @([[ "{{ VERS }}" == "$('{{ BIN_FILE }}' --version)" ]]) || \
        (echo '[FAIL] --version' && exit 2)

    @echo '[PASS] test-e2e-bin'

test-e2e-npm:
    @npm install --no-save --silent '{{ NPM_DIST }}'*

    @([[ "{{ VERS }}" == "$(npx --no poodio -- --version)" ]]) || \
        (echo '[FAIL] --version' && exit 2)
    @([[ "{{ VERS }}" == "$(node --print 'require("poodio").version()')" ]]) || \
        (echo '[FAIL] .version()' && exit 2)

    @npm prune --silent
    @echo '[PASS] test-e2e-npm'

test-e2e-pip:
    @uv pip install --reinstall --quiet '{{ PIP_DIST }}'*

    @([[ "{{ VERS }}" == "$(uv run --no-sync python -c 'import poodio as p;print(p.version())')" ]]) || \
        (echo '[FAIL] .version()' && exit 2)
    @# TODO: --version

    @uv pip uninstall --quiet poodio
    @echo '[PASS] test-e2e-pip'

BIN_NAME := 'poodio' + if os() == 'windows' { '.exe' } else { '' }
BIN_FILE := BIN_DIST + BIN_NAME
BIN_DIST := 'dist/bin/'
NPM_DIST := 'dist/npm/'
PIP_DIST := 'dist/pip/'
VERS := '$(cargo pkgid -p poodio | sed -E "s|.+/(.+)#|\1@|")'
