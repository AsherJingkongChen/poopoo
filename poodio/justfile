set shell := ['/usr/bin/env', 'bash', '-cu']
set windows-shell := ['C:/Program Files/Git/bin/bash.exe', '-cu']

build TARGET:
    @just \
        build-bin '{{ TARGET }}' \
        build-npm '{{ TARGET }}' \
        build-pip '{{ TARGET }}'

build-bin TARGET:
    rm -rf '{{ BIN_DIST }}'
    cargo build --bin poodio --locked \
        --features default --release --target '{{ TARGET }}'
    mkdir -p '{{ BIN_DIST }}'
    cp '../target/{{ TARGET }}/release/{{ BIN_NAME }}' '{{ BIN_FILE }}'
    @echo '[DIST] poodio/{{ BIN_DIST }}'

build-npm TARGET:
    rm -rf '{{ NPM_DIST }}'
    npx napi build --cargo-flags='--locked --lib' --no-dts-header --release \
        --features bind-napi --target '{{ TARGET }}' '{{ NPM_DIST }}native/'
    cp '{{ NPM_DIST }}native/index.d.ts' '{{ NPM_DIST }}common/'
    @echo '[DIST] poodio/{{ NPM_DIST }}'

build-pip TARGET:
    rm -rf '{{ PIP_DIST }}'
    uv run maturin build --bindings pyo3 --locked --release \
        --features bind-pyo3 --target '{{ TARGET }}' --out '{{ PIP_DIST }}'
    @echo '[DIST] poodio/{{ PIP_DIST }}'

test-e2e:
    @just \
        test-e2e-bin \
        test-e2e-npm \
        test-e2e-pip \
    && echo '[PASS] test-e2e'

test-e2e-bin:
    @[[ -x '{{ BIN_FILE }}' && ! -d '{{ BIN_FILE }}' ]] || \
        (echo '[FAIL] permission' && exit 2)
    @[[ "{{ VERS }}" == "$('{{ BIN_FILE }}' --version)" ]] || \
        (echo '[FAIL] --version' && exit 2)

    @echo '[PASS] test-e2e-bin'

test-e2e-npm:
    @# NOTE: it works in npm with node v16+
    @for try in {1..2}; do \
        npm install --no-save --silent '{{ NPM_DIST }}'* && break; \
    done

    @[[ "{{ VERS }}" == $(node --print 'require("poodio").version()') ]] || \
        (echo '[FAIL] .version()' && exit 2)
    @[[ "{{ VERS }}" == "$(npx --no poodio -- --version)" ]] || \
        (echo '[FAIL] --version' && exit 2)

    @cd '../' && npm prune
    @echo '[PASS] test-e2e-npm'

test-e2e-pip:
    @uv pip install --quiet './{{ PIP_DIST }}'*

    @[[ "{{ VERS }}" == "$(uv run python -c 'import poodio as p;print(p.version())')" ]] || \
        (echo '[FAIL] .version()' && exit 2)
    @# TODO: --version
    @[[ "{{ VERS }}" == "$(uv run poodio --version)" ]] || \
        (echo '[FAIL] --version' && exit 2)

    @uv pip uninstall poodio
    @echo '[PASS] test-e2e-pip'

hi:
    @echo $HI
BIN_NAME := 'poodio' + if os() == 'windows' { '.exe' } else { '' }
BIN_FILE := BIN_DIST + BIN_NAME
BIN_DIST := 'dist/bin/'
NPM_DIST := 'dist/npm/'
PIP_DIST := 'dist/pip/'
VERS := '$(cargo pkgid -p poodio | sed -E "s|.+/(.+)#|\1@|")'
