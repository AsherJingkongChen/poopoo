build TARGET:
    just \
    build-default '{{ TARGET }}' \
    build-bind-napi '{{ TARGET }}' \
    build-bind-pyo3 '{{ TARGET }}'

build-default TARGET:
    cargo build --locked --release --features default --target '{{ TARGET }}'
    @mkdir -p 'dist/bin/'
    cp '../target/{{ TARGET }}/release/poodio{{ BIN_EXT }}' 'dist/bin/'

build-bind-napi TARGET:
    @printf '{}' > 'package.json'
    npx napi build --cargo-flags=--locked --no-dts-header --release \
        --features bind-napi --target '{{ TARGET }}' 'dist/npm/bind/'
    @rm -f 'package.json'

    cp 'dist/npm/bind/index.d.ts' 'dist/npm/wrap/'
    @printf '{{ NPM_DIST_PURE_MAIN_DATA }}' > 'dist/npm/wrap/index.js'
    chmod +x 'dist/npm/wrap/index.js'

    npm install --no-save

build-bind-pyo3 TARGET:
    uv run maturin build --bindings pyo3 --locked --release \
        --features bind-pyo3 --target '{{ TARGET }}' --out 'dist/uv/'
    uv pip install 'dist/uv/'*

test SUBJECTS='':
    #! /usr/bin/env bash
    for s in {{ SUBJECTS }}; do npx @poodio/test-$s; done

BIN_EXT := if os() == 'windows' { '.exe' } else { '' }
NPM_DIST_PURE_MAIN_DATA := '''
#! /usr/bin/env node
require("tell-libc");
const { name } = require("./package.json");
const { argv, arch: cpu, platform: os, libc } = process;
const m = require(`@${name}/${name}-${cpu}-${os}-${libc || "unknown"}`);
module.exports = m;
require.main === module && m.main(argv.slice(1));
'''
