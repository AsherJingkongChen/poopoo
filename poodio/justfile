build TARGET:
    @printf '{}' > package.json
    npx napi build --cargo-flags=--locked --no-dts-header --release \
        --target '{{ TARGET }}' dist/npm/plat/
    @rm -f package.json

    @mkdir -p dist/bin/
    cp ../target/{{ TARGET }}/release/poodio{{ BIN_EXT }} \
        dist/bin/
    cp dist/npm/plat/index.d.ts dist/npm/pure/

    @printf '{{ NPM_DIST_PURE_MAIN_DATA }}' > dist/npm/pure/index.js
    chmod +x dist/npm/pure/index.js

    npm i --no-package-lock --no-save ./dist/npm/*/

test SUBJECTS:
    just $(for s in {{ SUBJECTS }}; do printf "test-$s "; done)

test-e2e-bin:
    node test/e2e/bin/index.cjs

test-e2e-npm:
    npx test/e2e/npm

BIN_EXT := if os() == 'windows' { '.exe' } else { '' }
NPM_DIST_PURE_MAIN_DATA := '''
#! /usr/bin/env node
require("tell-libc");
const { name } = require("./package.json");
const { argv, arch: cpu, platform: os, libc } = process;
const m = require(`@${name}/${name}-${cpu}-${os}-${libc || "unknown"}`);
module.exports = m;
require.main === module && m.main(argv.slice(1));
'''
